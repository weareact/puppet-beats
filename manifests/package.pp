# Install beats packages
class beats::package (
  $version = $beats::version,
){
  case $::osfamily {
    'Debian': {
      # Install pcap because it's useful and used in packetbeat
      package { 'libpcap0.8':
        ensure => installed,
      }

      if $beats::manage_geoip {
        package { 'geoip-database-contrib':
          ensure => latest,
        }
      }
    }
    'RedHat': {
      package { 'libpcap':
        ensure => installed,
      }

      if $beats::manage_geoip {

        if defined(Class['epel']) {
          $require_for_geoip = [ Class['epel'], ]
        }
        elsif defined(Package['epel-release']) {
          $require_for_geoip = [ Package['epel-release'], ]
        }
        else {
          package { 'epel-release':
            ensure => installed,
          }
          $require_for_geoip = [ Package['epel-release'], ]
        }

        package { 'GeoIP':
           ensure => latest,
           require => $require_for_geoip,
        }
      }

    }
    default: { fail("${::osfamily} not supported yet") }
  }
}
